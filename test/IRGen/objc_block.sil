// RUN: %target-swift-frontend %s -emit-ir | FileCheck %s

// REQUIRES: CPU=x86_64
// REQUIRES: objc_interop

sil_stage canonical

import Swift

class Foo {}
sil_vtable Foo {}

sil @_TFC10objc_block3FooD : $Foo -> ()

sil @call_block : $@thin (@cc(cdecl) @objc_block Foo -> Foo, Foo) -> Foo {
entry(%b : $@cc(cdecl) @objc_block Foo -> Foo, %x : $Foo):
  %y = apply %b(%x) : $@cc(cdecl) @objc_block Foo -> Foo
  return %y : $Foo
}
// CHECK-LABEL: define %C10objc_block3Foo* @call_block(%objc_block*, %C10objc_block3Foo*) {
// CHECK:       entry:
// CHECK:         %2 = getelementptr inbounds %objc_block, %objc_block* %0, i32 0, i32 3
// CHECK:         %3 = load i8*, i8** %2, align 8
// CHECK:         %4 = bitcast i8* %3 to i8* (%objc_block*, i8*)*
// CHECK:         %5 = bitcast %C10objc_block3Foo* %1 to i8*
// CHECK:         %6 = call i8* %4(%objc_block* %0, i8* %5)
// CHECK:         %7 = bitcast i8* %6 to %C10objc_block3Foo*
// CHECK:         ret %C10objc_block3Foo* %7
// CHECK:       }

sil @generic : $@thin <T> (@in T) -> ()

sil @generic_with_block : $@thin (@in @cc(cdecl) @objc_block () -> ()) -> () {
entry(%b : $*@cc(cdecl) @objc_block () -> ()):
  %f = function_ref @generic : $@thin <T> (@in T) -> ()
  %z = apply %f<@objc_block () -> ()>(%b) : $@thin <T> (@in T) -> ()
  return %z : $()
}

// CHECK-LABEL: define void @generic_with_block(%objc_block** noalias)
// CHECK:         call %swift.type* @swift_getBlockTypeMetadata

