// RUN: rm -rf %t/clang-module-cache
// RUN: %swift -target x86_64-apple-darwin10 -module-cache-path %t/clang-module-cache -sdk %S/Inputs -I=%S/Inputs -enable-source-import %s -emit-ir | FileCheck %s

import Swift
import gizmo

//
// A pure Swift class. We can access its isa directly.
//

class Purebred {
  func method() {}
}

// CHECK-LABEL: define void @purebred_method(%C18class_isa_pointers8Purebred*) {
// CHECK:         [[ISA_PTR:%.*]] = bitcast %C18class_isa_pointers8Purebred* %0 to %swift.type**
// CHECK:         [[ISA:%.*]] = load %swift.type** [[ISA_PTR]]
// CHECK:         [[VTABLE:%.*]] = bitcast %swift.type* [[ISA]]
// CHECK:         getelementptr inbounds {{.*}} [[VTABLE]]
sil @purebred_method : $@thin (@owned Purebred) -> () {
entry(%0 : $Purebred):
  %m = class_method %0 : $Purebred, #Purebred.method!1 : Purebred -> () -> (), $@cc(method) @thin (@owned Purebred) -> ()
  %z = apply %m(%0) : $@cc(method) @thin (@owned Purebred) -> ()
  return %z : $()
}

//
// A mixed Swift/ObjC class. We have to use object_getClass to access its isa.
//

class Mongrel: Gizmo {
  func method() {}
}

// CHECK-LABEL: define void @mongrel_method(%C18class_isa_pointers7Mongrel*) {
// CHECK:         [[AS_OBJC:%.*]] = bitcast {{.*}} %0 to %objc_object*
// CHECK:         [[ISA_CLASS:%.*]] = call %objc_class* @object_getClass(%objc_object* [[AS_OBJC]])
// CHECK:         [[ISA:%.*]] = bitcast %objc_class* [[ISA_CLASS]] to %swift.type*
// CHECK:         [[VTABLE:%.*]] = bitcast %swift.type* [[ISA]]
// CHECK:         getelementptr inbounds {{.*}} [[VTABLE]]
sil @mongrel_method : $@thin (@owned Mongrel) -> () {
entry(%0 : $Mongrel):
  %m = class_method %0 : $Mongrel, #Mongrel.method!1 : Mongrel -> () -> (), $@cc(method) @thin (@owned Mongrel) -> ()
  %z = apply %m(%0) : $@cc(method) @thin (@owned Mongrel) -> ()
  return %z : $()
}

// ObjC stubs expected by ObjC metadata emission

sil private @_TToFC18class_isa_pointers7Mongrel6methodfS0_FT_T_ : $@thin @cc(objc_method) (Purebred) -> () {
entry(%0 : $Purebred):
  unreachable
}
sil private @_TToFC18class_isa_pointers7MongrelcfMS0_FT11withBellsOnSi_S0_ : $@thin @cc(objc_method) (Int, Purebred) -> () {
entry(%0 : $Int, %1 : $Purebred):
  unreachable
}
sil private @_TToFC18class_isa_pointers7MongrelcfMS0_FT_S0_ : $@thin @cc(objc_method) (Purebred) -> () {
entry(%0 : $Purebred):
  unreachable
}
