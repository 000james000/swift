// RUN: %swift -target x86_64-apple-darwin10 %s -emit-ir | FileCheck %s

import Swift

// CHECK-LABEL: define void @archetype_metatype_cast
// CHECK:         %0 = call %swift.type* @swift_dynamicCastMetatype(%swift.type* %T, %swift.type* %U) #0
// CHECK:         %1 = icmp ne %swift.type* %0, null
sil @archetype_metatype_cast : $@thin <T, U> () -> () {
entry:
  %0 = metatype $@thick T.Type
  checked_cast_br archetype_to_archetype %0 : $@thick T.Type to $@thick U.Type, yes, no
yes(%1 : $@thick U.Type):
  br end

no:
  br end

end:
  return undef : $()
}

protocol P {}

// CHECK-LABEL: define void @existential_archetype_metatype_cast(%swift.type*, %swift.type* %T)
// CHECK:         %1 = call %swift.type* @swift_dynamicCastMetatype(%swift.type* %0, %swift.type* %T) #0
// CHECK:         %2 = icmp ne %swift.type* %1, null
sil @existential_archetype_metatype_cast : $@thin <T> (@thick P.Type) -> () {
entry(%0 : $@thick P.Type):
  checked_cast_br existential_to_archetype %0 : $@thick P.Type to $@thick T.Type, yes, no
yes(%1 : $@thick T.Type):
  br end

no:
  br end

end:
  return undef : $()
}

class SomeClass {}

// CHECK-LABEL: define %objc_object* @metatype_object_conversion(%objc_class*) {
// CHECK:         %1 = bitcast %objc_class* %0 to %objc_object*
// CHECK:         ret %objc_object* %1
// CHECK:       }
sil @metatype_object_conversion : $@thin (@objc_metatype SomeClass.Type) -> @owned AnyObject {
entry(%m : $@objc_metatype SomeClass.Type):
  %o = objc_metatype_to_object %m : $@objc_metatype SomeClass.Type to $AnyObject
  return %o : $AnyObject
}

@class_protocol protocol SomeClassProtocol {}

// CHECK-LABEL: define %objc_object* @existential_metatype_object_conversion(%objc_class*) {
// CHECK:         %1 = bitcast %objc_class* %0 to %objc_object*
// CHECK:         ret %objc_object* %1
// CHECK:       }
sil @existential_metatype_object_conversion : $@thin (@objc_metatype SomeClassProtocol.Type) -> @owned AnyObject {
entry(%m : $@objc_metatype SomeClassProtocol.Type):
  %o = objc_existential_metatype_to_object %m : $@objc_metatype SomeClassProtocol.Type to $AnyObject
  return %o : $AnyObject
}

