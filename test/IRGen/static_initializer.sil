// RUN: %swift -target x86_64-apple-macosx10.9 %s -emit-ir | FileCheck %s

// Generated from
// var x : Int = 2
// public func f() -> Int { return x }
// with swiftc -O -parse-as-library -emit-sil

import Builtin
import Swift

sil_global private @globalinit_token0 : $Builtin.Word
// CHECK: @globalinit_token0 = internal global i64 0, align 8

sil_global @_Tv2ch1xSi : $Int, @globalinit_func0 : $@thin () -> ()
// CHECK: @_Tv2ch1xSi = global %Si zeroinitializer, align 8

// CHECK: @llvm.global_ctors = appending global [1 x { i32, void ()*, i8* }] [{ i32, void ()*, i8* } { i32 65535, void ()* @_GLOBAL__static_initializer, i8* null }]

// CHECK-LABEL: define internal void @globalinit_func0() {
// CHECK-NEXT: entry:
// CHECK-NEXT: store i64 2, i64* getelementptr inbounds (%Si* @_Tv2ch1xSi, i32 0, i32 0)
// CHECK-NEXT: ret
sil private @globalinit_func0 : $@thin () -> () {
bb0:
  %0 = sil_global_addr @_Tv2ch1xSi : $*Int
  %1 = integer_literal $Builtin.Word, 2
  %2 = struct $Int (%1 : $Builtin.Word)
  store %2 to %0 : $*Int
  %4 = tuple ()
  return %4 : $()
}

// CHECK-LABEL: define i8* @_TF2cha1xSi() {
// CHECK-NEXT: entry:
// CHECK-NEXT: ret i8* bitcast (%Si* @_Tv2ch1xSi to i8*)
sil [global_init] @_TF2cha1xSi : $@thin () -> Builtin.RawPointer {
bb0:
  %0 = sil_global_addr @_Tv2ch1xSi : $*Int
  %1 = address_to_pointer %0 : $*Int to $Builtin.RawPointer
  return %1 : $Builtin.RawPointer
}

// CHECK-LABEL: define i64 @_TF2ch1fFT_Si() {
// CHECK-NEXT: entry:
// CHECK-NEXT: load i64* getelementptr inbounds (%Si* @_Tv2ch1xSi, i32 0, i32 0)
// CHECK-NEXT: ret
sil @_TF2ch1fFT_Si : $@thin () -> Int {
bb0:
  %0 = sil_global_addr @_Tv2ch1xSi : $*Int
  %1 = load %0 : $*Int
  return %1 : $Int
}

// CHECK-LABEL: define internal void @_GLOBAL__static_initializer() {
// CHECK-NEXT: entry:
// CHECK-NEXT: call void @globalinit_func0()
// CHECK-NEXT: ret
