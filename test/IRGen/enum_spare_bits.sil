// RUN: rm -rf %t/clang-module-cache
// RUN: %swift -target x86_64-apple-macosx10.9 -module-cache-path %t/clang-module-cache -sdk %S/Inputs -I=%S/Inputs -enable-source-import %s -emit-ir | FileCheck %s

import Swift
import Foundation

class C {}
class D: NSObject {}

enum SwiftClass {
  case A(C), B(D)
}
// can use spare bits—both payloads are Swift-defined classes
// CHECK: %O15enum_spare_bits10SwiftClass = type <{ [8 x i8] }>

enum ObjCClass {
  case A(NSObject), B(C)
}
// can't use spare bits—NSObject is an ObjC-defined class
// CHECK: %O15enum_spare_bits9ObjCClass = type <{ [8 x i8], [1 x i8] }>

@objc @unsafe_no_objc_tagged_pointer
protocol NoTaggedPointers {}

enum Existential {
  case A(AnyObject), B(C)
}
// can't use spare bits—existential may be bound to tagged pointer type
// CHECK: %O15enum_spare_bits11Existential = type <{ [8 x i8], [1 x i8] }>

enum ExistentialNoTaggedPointers {
  case A(NoTaggedPointers), B(C)
}
// can use spare bits—@unsafe_no_objc_tagged_pointer says it's ok
// CHECK: %O15enum_spare_bits27ExistentialNoTaggedPointers = type <{ [8 x i8] }>

enum Archetype<T: AnyObject> {
  case A(T), B(C)
}
// can't use spare bits—archetype may be bound to tagged pointer type
// CHECK: [[ARCHETYPE:%O15enum_spare_bits9Archetype.*]] = type <{ [8 x i8], [1 x i8] }>

sil_global @swiftClass: $SwiftClass
sil_global @objcClass: $ObjCClass
sil_global @existential: $Existential
sil_global @existentialntp: $ExistentialNoTaggedPointers
sil_global @archetypeBoundToSwift: $Archetype<C>
sil_global @archetypeBoundToObjC: $Archetype<NSObject>

// CHECK: @archetypeBoundToSwift = global [[ARCHETYPE]]
// CHECK: @archetypeBoundToObjC = global [[ARCHETYPE]]

sil @instantiate_globals : $() -> () {
entry:
  %a = sil_global_addr @swiftClass : $*SwiftClass
  %b = sil_global_addr @objcClass : $*ObjCClass
  %c = sil_global_addr @existential : $*Existential
  %d = sil_global_addr @existentialntp : $*ExistentialNoTaggedPointers
  %e = sil_global_addr @archetypeBoundToSwift : $*Archetype<C>
  %f = sil_global_addr @archetypeBoundToObjC : $*Archetype<NSObject>
  return undef : $()
}

sil @_TToFC15enum_spare_bits1DcfMS0_FT_S0_ : $(@owned D) -> @owned D {
entry(%x : $D):
  return undef : $D
}
