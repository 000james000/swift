// RUN: %sil-opt %s -memory-promotion -verify | FileCheck %s

import Builtin
import swift


// CHECK-LABEL: sil @simple_reg_promotion
sil @simple_reg_promotion : $[thin] (a : Int64) -> Int64 {
bb0(%0 : $Int64):                         // CHECK: bb0(%0 : $Int64):
  %1 = alloc_box $Int64                   // CHECK: alloc_box
  store %0 to %1#1 : $*Int64
  %3 = alloc_box $Int64                   // CHECK: alloc_box
  %4 = load %1#1 : $*Int64
  store %4 to %3#1 : $*Int64         // CHECK-NEXT: store
  %6 = load %3#1 : $*Int64
  release %3#0 : $Builtin.ObjectPointer   // CHECK-NEXT: release
  release %1#0 : $Builtin.ObjectPointer
  return %6 : $Int64

  // CHECK: return %0 : $Int64
}


// CHECK-LABEL: sil @tuple_reg_promotion
sil @tuple_reg_promotion : $[thin] (a : Int64) -> Int64 {
bb0(%0 : $Int64):                         // CHECK: bb0(%0 : $Int64):
  %1 = alloc_box $(Int64, Int64)                   // CHECK: alloc_box
  
  %a = tuple_element_addr %1#1 : $*(Int64, Int64), 0
  %b = tuple_element_addr %1#1 : $*(Int64, Int64), 1
  store %0 to %a : $*Int64
  // CHECK: store %0 to

  store %0 to %b : $*Int64
  // CHECK-NEXT: store %0 to 

  %c = load %1#1 : $*(Int64, Int64)
  %d = tuple_extract %c : $(Int64, Int64), 0

  release %1#0 : $Builtin.ObjectPointer

  return %d : $Int64

  // Verify that promotion has promoted the tuple load away, and we know that
  // %0 is being returned through scalar instructions in SSA form.
  // CHECK: [[TUPLE:%[0-9]+]] = tuple (%0 : $Int64, %0 : $Int64)
  // CHECK: [[TUPLE_ELT:%[0-9]+]] = tuple_extract [[TUPLE]] : $(Int64, Int64), 0

  // CHECK: return [[TUPLE_ELT]] : $Int64
}


// CHECK-LABEL: sil @use_before_init
sil @use_before_init : $[thin] () -> Int64 {
bb0:
  %1 = alloc_box $Int64                   // expected-note {{variable defined here}}
  %4 = load %1#1 : $*Int64                // expected-error {{variable '<unknown>' used before being initialized}}
  release %1#0 : $Builtin.ObjectPointer
  %9 = return %4 : $Int64
}


sil @takes_Int_byref : $[thin] (a : [byref] Int64) -> ()

// CHECK-LABEL: @byref_uninit
sil @byref_uninit : $[thin] () -> () {
bb0:
  %1 = alloc_box $Int64                                   // expected-note {{variable defined here}}

  %5 = function_ref @takes_Int_byref : $[thin] (a : [byref] Int64) -> ()
  %6 = apply %5(%1#1) : $[thin] (a : [byref] Int64) -> () // expected-error {{variable '<unknown>' passed by reference before being initialized}}
  
  %0 = tuple ()
  release %1#0 : $Builtin.ObjectPointer
  return %0 : $()
}




// This function shouldn't produce any diagnostics.
//
// func used_by_byref(a : Int) -> (Int, Int) {
//  var t = a
//  takes_Int_byref(&a)
//  return (t,a)
//}
// CHECK-LABEL: sil @used_by_byref
sil @used_by_byref : $[thin] (a : Int64) -> (Int64, Int64) {
bb0(%0 : $Int64):
  // This alloc_stack can't be removed since it is used by a byref call.
  // CHECK: %1 = alloc_box $Int64
  %1 = alloc_box $Int64
  %2 = store %0 to %1#1 : $*Int64
  
  // This load should be eliminated.
  %3 = load %1#1 : $*Int64
  %5 = function_ref @takes_Int_byref : $[thin] (a : [byref] Int64) -> ()
  %6 = apply %5(%1#1) : $[thin] (a : [byref] Int64) -> ()
  
  // This load is needed in case the callee modifies the allocation.
  // CHECK: [[RES:%[0-9]+]] = load %1
  %7 = load %1#1 : $*Int64
  
  // This should use the incoming argument to the function.
  // CHECK: tuple (%0 : $Int64, [[RES]] : $Int64)
  %8 = tuple (%3 : $Int64, %7 : $Int64)
  release %1#0 : $Builtin.ObjectPointer
  %11 = return %8 : $(Int64, Int64)
}


struct GenericStruct<T> {
  var a : T
  var b : Int
}

/// returns_generic_struct - This returns a struct by reference.
sil @returns_generic_struct : $[thin] () -> GenericStruct<Int64>

// There should be no error in this function.
// CHECK-LABEL: sil @call_struct_return_function
sil @call_struct_return_function : $[thin] () -> Int64 {
bb0:
  %1 = alloc_box $GenericStruct<Int64>
  %2 = function_ref @returns_generic_struct : $[thin] () -> GenericStruct<Int64>
  %3 = apply %2(%1#1) : $[thin] () -> GenericStruct<Int64>
  %4 = struct_element_addr %1#1 : $*GenericStruct<Int64>, #b
  %5 = load %4 : $*Int64
  release %1#0 : $Builtin.ObjectPointer
  return %5 : $Int64
}


// CHECK-LABEL: sil @tuple_elements1
sil @tuple_elements1 : $[thin] (a : Int64) -> () {
bb0(%0 : $Int64):
  %3 = alloc_box $(Int64, Int64)     // expected-note {{variable defined here}}
  %4 = tuple_element_addr %3#1 : $*(Int64, Int64), 0
  %5 = tuple_element_addr %3#1 : $*(Int64, Int64), 1
  %14 = function_ref @takes_Int_byref : $[thin] (a : [byref] Int64) -> ()
  %15 = tuple_element_addr %3#1 : $*(Int64, Int64), 1
  %16 = apply %14(%15) : $[thin] (a : [byref] Int64) -> ()  // expected-error {{variable '<unknown>.1' passed by reference before being initialized}}

  release %3#0 : $Builtin.ObjectPointer
  %99 = tuple ()
  return %99 : $()
}

// CHECK-LABEL: sil @tuple_elements2
sil @tuple_elements2 : $[thin] (a : Int64) -> (Int64, Int64) {
bb0(%0 : $Int64):
  %3 = alloc_box $(Int64, Int64)     // expected-note {{variable defined here}}
  %18 = tuple_element_addr %3#1 : $*(Int64, Int64), 0
  store %0 to %18 : $*Int64
  %20 = load %3#1 : $*(Int64, Int64) // expected-error {{variable '<unknown>.1' used before being initialized}}
  %21 = tuple_extract %20 : $(Int64, Int64), 0
  %22 = tuple_extract %20 : $(Int64, Int64), 1
  %23 = tuple (%21 : $Int64, %22 : $Int64)
  release %3#0 : $Builtin.ObjectPointer
  return %23 : $(Int64, Int64)
}

struct XYStruct { var x, y : Int }


// CHECK-LABEL: sil @copy_addr1
sil @copy_addr1 : $[thin] <T> (v : T) -> T {
bb0(%0 : $*T, %1 : $*T):
  %4 = alloc_box $T
  copy_addr [take] %1 to [initialization] %4#1 : $*T
  copy_addr %4#1 to [initialization] %0 : $*T
  release %4#0 : $Builtin.ObjectPointer
  %9 = tuple ()
  return %9 : $()
}

// CHECK-LABEL: sil @copy_addr2
sil @copy_addr2 : $[thin] <T> (v : T) -> T {
bb0(%0 : $*T, %1 : $*T):
  %4 = alloc_box $T         // expected-note {{variable defined here}}
  copy_addr %4#1 to [initialization] %0 : $*T   // expected-error {{variable '<unknown>' used before being initialized}}
  release %4#0 : $Builtin.ObjectPointer
  %9 = tuple ()
  return %9 : $()
}


sil @takes_closure : $[thin] (x : () -> ()) -> ()
sil @closure0 : $[thin] ((), (Builtin.ObjectPointer, [byref] Int64)) -> ()

// CHECK-LABEL: sil @closure_test
sil @closure_test : $[thin] () -> () {
bb0:
  %0 = alloc_box $Int64    // expected-note {{variable defined here}}

  %5 = function_ref @takes_closure : $[thin] (x : () -> ()) -> ()
  %6 = function_ref @closure0 : $[thin] ((), (Builtin.ObjectPointer, [byref] Int64)) -> ()
  retain %0#0 : $Builtin.ObjectPointer
  %8 = partial_apply %6(%0#0, %0#1) : $[thin] ((), (Builtin.ObjectPointer, [byref] Int64)) -> ()  // expected-error {{variable '<unknown>' captured by a closure before being initialized}}
  %9 = apply %5(%8) : $[thin] (x : () -> ()) -> ()
  release %0#0 : $Builtin.ObjectPointer

  %11 = tuple ()
  return %11 : $()
}

class SomeClass {}

sil @getSomeClass : $[thin] ((), SomeClass.metatype) -> SomeClass


// CHECK-LABEL: sil @assign_test_trivial
sil @assign_test_trivial : $[thin] (a : Int64) -> Int64 {
bb0(%0 : $Int64):
  %1 = alloc_box $Int64

  // These assigns are a mix of init + store forms, but because Int is trivial,
  // they all turn into stores.
  assign %0 to %1#1 : $*Int64                      // CHECK: store %0 to %1#1
  assign %0 to %1#1 : $*Int64                      // CHECK: store %0 to %1#1
  assign %0 to %1#1 : $*Int64                      // CHECK: store %0 to %1#1

  %2 = load %1#1 : $*Int64
  release %1#0 : $Builtin.ObjectPointer            // CHECK-NEXT: release %1#0

  // Verify that the load got forwarded from an assign.
  return %2 : $Int64                        // CHECK: return %0 : $Int64
}

// CHECK-LABEL: sil @assign_test_nontrivial
sil @assign_test_nontrivial : $[thin] () -> () {
bb0:
  // Assignments of nontrivial types.  The first becomes an initialize (i.e.,
  // lone store), the second becomes an assignment (retain/release dance).

  %c = alloc_box $SomeClass
  %f = function_ref @getSomeClass : $[thin] ((), SomeClass.metatype) -> SomeClass
  %3 = metatype $SomeClass.metatype

  %4 = apply %f(%3) : $[thin] ((), SomeClass.metatype) -> SomeClass
  // CHECK: [[C1:%[0-9]+]] = apply

  assign %4 to %c#1 : $*SomeClass
  // CHECK-NEXT: store [[C1]]
  // CHECK-NEXT: retain [[C1]]

  release %4 : $SomeClass
  // CHECK-NEXT: release [[C1]]

  %8 = apply %f(%3) : $[thin] ((), SomeClass.metatype) -> SomeClass
  // CHECK-NEXT: [[C2:%[0-9]+]] = apply

  assign %8 to %c#1 : $*SomeClass
  // CHECK-NEXT: retain [[C2]]
  // CHECK-NEXT: store [[C2]]
  // CHECK-NEXT: release [[C1]]

  release %8 : $SomeClass
  // CHECK-NEXT: release [[C2]]

  release %c#0 : $Builtin.ObjectPointer

  %11 = tuple ()
  return %11 : $()
}


// CHECK-LABEL: sil @assign_test_addressonly
sil @assign_test_addressonly : $[thin] <T> (v : T) -> T {
bb0(%0 : $*T, %1 : $*T):
  %2 = alloc_box $T
  // CHECK: alloc_box

  // This should become an initialization of %4
  copy_addr %1 to %2#1 : $*T
  // CHECK-NEXT: copy_addr %1 to [initialization] %2#1 : $*T

  // This should stay an assignment of %4
  copy_addr [take] %1 to %2#1 : $*T
  // CHECK-NEXT: copy_addr [take] %1 to %2#1 : $*T
  
  // This is a load, and shouldn't be changed.
  copy_addr %2#1 to [initialization] %0 : $*T
  // CHECK-NEXT: copy_addr %2#1 to [initialization] %0 : $*T

  release %2#0 : $Builtin.ObjectPointer
  // CHECK-NEXT: release %2#0
  %9 = tuple ()
  return %9 : $()
}

// CHECK-LABEL: sil @assign_test_weak
sil @assign_test_weak : $[thin]() -> () {
bb0:
  // Assignments of weak pointer.  The first becomes an initialize, and the
  // second becomes an assignment.

  %c = alloc_box $[weak] SomeClass
  %f = function_ref @getSomeClass : $[thin] ((), SomeClass.metatype) -> SomeClass
  %3 = metatype $SomeClass.metatype

  %4 = apply %f(%3) : $[thin] ((), SomeClass.metatype) -> SomeClass
  // CHECK: [[C1:%[0-9]+]] = apply

  // THis should become an initialization.
  store_weak %4 to %c#1 : $*[weak] SomeClass
  // CHECK-NEXT: store_weak [[C1]] to [initialization] %0#1

  release %4 : $SomeClass
  // CHECK-NEXT: release [[C1]]

  %8 = apply %f(%3) : $[thin] ((), SomeClass.metatype) -> SomeClass
  // CHECK-NEXT: [[C2:%[0-9]+]] = apply

  store_weak %8 to %c#1 : $*[weak] SomeClass
  // CHECK-NEXT: store_weak [[C2]] to %0#1

  release %8 : $SomeClass
  // CHECK-NEXT: release [[C2]]

  release %c#0 : $Builtin.ObjectPointer

  %11 = tuple ()
  return %11 : $()
}

// CHECK-LABEL: sil @assign_test_unowned
sil @assign_test_unowned : $[thin]() -> () {
bb0:
  // Assignments of unowned pointer.  The first becomes an initialize, and the
  // second becomes an assignment.

  %c = alloc_box $[unowned] SomeClass
  // CHECK: [[BOX:%[0-9]+]] = alloc_box
  %f = function_ref @getSomeClass : $[thin] ((), SomeClass.metatype) -> SomeClass
  %3 = metatype $SomeClass.metatype

  %4 = apply %f(%3) : $[thin] ((), SomeClass.metatype) -> SomeClass
  // CHECK: [[C1x:%[0-9]+]] = apply

  %5 = ref_to_unowned %4 : $SomeClass to $[unowned] SomeClass
  // CHECK-NEXT: [[C1:%[0-9]+]] = ref_to_unowned [[C1x]]

  // This should become an initialization.
  assign %5 to %c#1 : $*[unowned] SomeClass
  // CHECK-NEXT: store [[C1]] to [[BOX]]#1
  // CHECK-NEXT: unowned_retain [[C1]]

  release %4 : $SomeClass
  // CHECK-NEXT: release [[C1x]]

  %8 = apply %f(%3) : $[thin] ((), SomeClass.metatype) -> SomeClass
  // CHECK-NEXT: [[C2x:%[0-9]+]] = apply

  %9 = ref_to_unowned %8 : $SomeClass to $[unowned] SomeClass
  // CHECK-NEXT: [[C2:%[0-9]+]] = ref_to_unowned [[C2x]]

  assign %9 to %c#1 : $*[unowned] SomeClass
  // CHECK-NEXT: unowned_retain [[C2]]
  // CHECK-NEXT: store [[C2]] to [[BOX]]#1
  // CHECK-NEXT: unowned_release [[C1]]

  release %8 : $SomeClass
  // CHECK-NEXT: release [[C2x]]

  release %c#0 : $Builtin.ObjectPointer

  %11 = tuple ()
  return %11 : $()
}




