// RUN: %sil-opt -enum-simplification -verify %s | FileCheck %s

import Builtin

enum FakeOptional {
  case None
  case Some(Builtin.Int32)
}

enum Either {
  case First(Builtin.Int32)
  case Second(Builtin.Int64)
}

enum ThreeCaseEnum {
  case A
  case B
  case C
}

class C {}
enum FakeOptionalClass {
  case None
  case Some(C)
}

// CHECK-LABEL: sil @test1 : $@thin (FakeOptional) -> () {
// CHECK: bb0
// CHECK-NEXT: retain_value
// CHECK-NEXT: release_value
sil @test1 : $@thin (FakeOptional) -> () {
bb0(%0 : $FakeOptional):
  retain_value %0 : $FakeOptional
  release_value %0 : $FakeOptional
  %9999 = tuple()
  return %9999 : $()
}

// CHECK-LABEL: sil @test2 : $@thin (FakeOptional) -> () {
// CHECK: bb0
// CHECK-NEXT: unchecked_enum_data
// CHECK-NEXT: unchecked_enum_data
// CHECK-NEXT: retain_value
// CHECK-NEXT: unchecked_enum_data
// CHECK-NEXT: release_value
sil @test2 : $@thin (FakeOptional) -> () {
bb0(%0 : $FakeOptional):
  %1 = unchecked_enum_data %0 : $FakeOptional, #FakeOptional.Some!enumelt.1
  retain_value %0 : $FakeOptional
  release_value %0 : $FakeOptional
  %9999 = tuple()
  return %9999 : $()
}

// CHECK-LABEL: sil @test3 : $@thin () -> () {
// CHECK: bb0
// CHECK-NEXT: enum
// CHECK-NEXT: tuple
// CHECK-NEXT: return
sil @test3 : $@thin () -> () {
bb0:
  %0 = enum $FakeOptional, #FakeOptional.None!enumelt
  retain_value %0 : $FakeOptional
  release_value %0 : $FakeOptional
  %9999 = tuple()
  return %9999 : $()
}

// CHECK-LABEL: sil @test4 : $@thin () -> () {
// CHECK: bb0
// CHECK-NEXT: integer_literal
// CHECK-NEXT: enum
// CHECK-NEXT: unchecked_enum_data
// CHECK-NEXT: retain_value
// CHECK-NEXT: unchecked_enum_data
// CHECK-NEXT: release_value
sil @test4 : $@thin () -> () {
bb0:
  %0 = integer_literal $Builtin.Int32, 0
  %1 = enum $FakeOptional, #FakeOptional.Some!enumelt.1, %0 : $Builtin.Int32
  retain_value %1 : $FakeOptional
  release_value %1 : $FakeOptional
  %9999 = tuple()
  return %9999 : $()
}

// CHECK-LABEL: sil @test5 : $@thin () -> () {
// CHECK: bb1:
// CHECK-NEXT: [[DATA:%[0-9]+]] = unchecked_enum_data
// CHECK-NEXT: retain_value [[DATA]]
// CHECK: bb2:
// CHECK-NEXT: [[DATA1:%[0-9]+]] = unchecked_enum_data
// CHECK-NEXT: retain_value [[DATA1]]
sil @test5 : $@thin () -> () {
bb0:
  %0 = integer_literal $Builtin.Int32, 0
  %1 = enum $FakeOptional, #FakeOptional.Some!enumelt.1, %0 : $Builtin.Int32
  cond_br undef, bb1, bb2

bb1:
  retain_value %1 : $FakeOptional
  br bb3

bb2:
  retain_value %1 : $FakeOptional
  br bb3

bb3:
  %9999 = tuple()
  return %9999 : $()
}

// CHECK-LABEL: sil @test6 : $@thin (FakeOptional) -> () {
// CHECK: bb1:
// CHECK-NEXT: [[DATA:%[0-9]+]] = unchecked_enum_data
// CHECK-NEXT: retain_value [[DATA]]
// CHECK: bb2:
// CHECK-NEXT: [[DATA1:%[0-9]+]] = unchecked_enum_data
// CHECK-NEXT: retain_value [[DATA1]]
sil @test6 : $@thin (FakeOptional) -> () {
bb0(%0 : $FakeOptional):
  %1 = unchecked_enum_data %0 : $FakeOptional, #FakeOptional.Some!enumelt.1
  cond_br undef, bb1, bb2

bb1:
  retain_value %0 : $FakeOptional
  br bb3

bb2:
  retain_value %0 : $FakeOptional
  br bb3

bb3:
  %9999 = tuple()
  return %9999 : $()  
}

// CHECK-LABEL: sil @test7 : $@thin () -> () {
// CHECK-NOT: retain_value
// CHECK-NOT: retain_value
sil @test7 : $@thin () -> () {
bb0:
  %0 = enum $FakeOptional, #FakeOptional.None!enumelt
  cond_br undef, bb1, bb2

bb1:
  retain_value %0 : $FakeOptional
  br bb3

bb2:
  retain_value %0 : $FakeOptional
  br bb3

bb3:
  %9999 = tuple()
  return %9999 : $()
}

// CHECK-LABEL: sil @test8 : $@thin (FakeOptional) -> () {
// CHECK: bb0([[INPUT:%[0-9]+]] : $FakeOptional):
// CHECK: bb2:
// CHECK-NEXT: retain_value [[INPUT]]
// CHECK: bb3:
// CHECK-NEXT: [[PAYLOAD:%[0-9]+]] = unchecked_enum_data [[INPUT]]
// CHECK-NEXT: retain_value [[PAYLOAD]]
// CHECK: bb4:
// CHECK-NEXT: retain_value [[INPUT]]
sil @test8 : $@thin (FakeOptional) -> () {
bb0(%0 : $FakeOptional):
  cond_br undef, bb1, bb2

bb1:
  %1 = unchecked_enum_data %0 : $FakeOptional, #FakeOptional.Some!enumelt.1
  cond_br undef, bb2, bb3

bb2:
  retain_value %0 : $FakeOptional
  br bb4

bb3:
  retain_value %0 : $FakeOptional
  br bb4

bb4:
  retain_value %0 : $FakeOptional
  %9999 = tuple()
  return %9999 : $()
}

// CHECK-LABEL: sil @test9 : $@thin (FakeOptional) -> () {
// CHECK: bb0([[INPUT:%[0-9]+]] : $FakeOptional):
// CHECK: bb1:
// CHECK-NEXT: [[PAYLOAD:%[0-9]+]] = unchecked_enum_data [[INPUT]]
// CHECK-NEXT: retain_value [[PAYLOAD]]
// CHECK: bb2:
// CHECK-NOT: retain_value
// CHECK: bb3:
// CHECK-NEXT: retain_value [[INPUT]]
sil @test9 : $@thin (FakeOptional) -> () {
bb0(%0 : $FakeOptional):
  switch_enum %0 : $FakeOptional, case #FakeOptional.Some!enumelt.1: bb1, case #FakeOptional.None!enumelt: bb2

bb1:
  retain_value %0 : $FakeOptional
  br bb3

bb2:
  retain_value %0 : $FakeOptional
  br bb3

bb3:
  retain_value %0 : $FakeOptional
  %9999 = tuple()
  return %9999 : $()
}

////////////////
// Loop Tests //
////////////////

// CHECK-LABEL: sil @test10 : $@thin (FakeOptional) -> () {
// CHECK: bb0([[INPUT:%[0-9]+]] : $FakeOptional):
// CHECK: retain_value [[INPUT]]
// CHECK: retain_value [[INPUT]]
// CHECK: retain_value [[INPUT]]
// CHECK: retain_value [[INPUT]]
sil @test10 : $@thin (FakeOptional) -> () {
bb0(%0 : $FakeOptional):
  switch_enum %0 : $FakeOptional, case #FakeOptional.Some!enumelt.1: bb1, case #FakeOptional.None!enumelt: bb2

// Single BB Loop
bb1:
  retain_value %0 : $FakeOptional
  cond_br undef, bb1, bb9999

// Two BB Loop. We can use loop or domination to handle this case. But we don't
// handle it now.
bb2:
  retain_value %0 : $FakeOptional
  br bb3

bb3:
  retain_value %0 : $FakeOptional
  cond_br undef, bb2, bb9999

bb9999:
  retain_value %0 : $FakeOptional
  %9999 = tuple()
  return %9999 : $()
}

// Make sure we don't propagate state out of loops.
// CHECK-LABEL: sil @test11 : $@thin (FakeOptional) -> () {
// CHECK: bb0([[INPUT:%[0-9]+]] : $FakeOptional):
// CHECK: retain_value [[INPUT]]
// CHECK: retain_value [[INPUT]]
sil @test11 : $@thin (FakeOptional) -> () {
bb0(%0 : $FakeOptional):
  switch_enum %0 : $FakeOptional, case #FakeOptional.Some!enumelt.1: bb1, case #FakeOptional.None!enumelt: bb2

bb1:
  retain_value %0 : $FakeOptional
  cond_br undef, bb1, bb2

bb2:
  retain_value %0 : $FakeOptional
  %9999 = tuple()
  return %9999 : $()
}

// CHECK-LABEL: sil @test12 : $@thin (FakeOptional) -> () {
// CHECK: bb1:
// CHECK: release_value
// CHECK: bb2:
// CHECK-NOT: release_value
// CHECK: bb3:
// CHECK-NOT: release_value
sil @test12 : $@thin (FakeOptional) -> () {
bb0(%0 : $FakeOptional):
  switch_enum %0 : $FakeOptional, case #FakeOptional.Some!enumelt.1: bb1, case #FakeOptional.None!enumelt: bb2

bb1:
  br bb3

bb2:
  br bb3

bb3:
  release_value %0 : $FakeOptional
  %9999 = tuple()
  return %9999 : $()
}

// CHECK-LABEL: sil @test13 : $@thin (Either) -> () {
// CHECK: bb1:
// CHECK: release_value
// CHECK: bb2:
// CHECK: release_value
// CHECK: bb3:
// CHECK-NOT: release_value
sil @test13 : $@thin (Either) -> () {
bb0(%0 : $Either):
  cond_br undef, bb1, bb2

bb1:
  %1 = unchecked_enum_data %0 : $Either, #Either.First!enumelt.1
  br bb3

bb2:
  %2 = unchecked_enum_data %0 : $Either, #Either.Second!enumelt.1
  br bb3

bb3:
  release_value %0 : $Either
  %9999 = tuple()
  return %9999 : $()
}

// CHECK-LABEL: sil @test14 : $@thin (Either) -> () {
// CHECK: bb0(
// CHECK-NOT: release_value
// CHECK: bb1:
// CHECK: release_value
sil @test14 : $@thin (Either) -> () {
bb0(%0 : $Either):
  %1 = unchecked_enum_data %0 : $Either, #Either.First!enumelt.1
  cond_br undef, bb1, bb2

bb1:
  release_value %0 : $Either
  br bb3

bb2:
  br bb3

bb3:
  %9999 = tuple()
  return %9999 : $()
}

// CHECK-LABEL: sil @test15 : $@thin (Either, ThreeCaseEnum) -> () {
// CHECK: bb1:
// CHECK: release_value
// CHECK: bb2:
// CHECK-NOT: release_value
// CHECK: bb3:
// CHECK-NOT: release_value
sil @test15 : $@thin (Either, ThreeCaseEnum) -> () {
bb0(%0 : $Either, %1 : $ThreeCaseEnum):
  %2 = unchecked_enum_data %0 : $Either, #Either.First!enumelt.1
  switch_enum %1 : $ThreeCaseEnum, case #ThreeCaseEnum.A!enumelt: bb1,
                  case #ThreeCaseEnum.B!enumelt: bb2,
                  case #ThreeCaseEnum.C!enumelt: bb3

bb1:
  release_value %0 : $Either
  br bb4

bb2:
  br bb4

bb3:
  br bb4

bb4:
  %9999 = tuple()
  return %9999 : $()
}

sil @unknown : $@thin () -> ()

// CHECK-LABEL: sil @test16 : $@thin (FakeOptionalClass) -> () {
// CHECK: bb1:
// CHECK-NOT: release_value
// CHECK: bb2:
// CHECK: unchecked_enum_data
// CHECK: release_value
// CHECK: bb3
// CHECK: release_value
sil @test16 : $@thin (FakeOptionalClass) -> () {
bb0(%0 : $FakeOptionalClass):
  switch_enum %0 : $FakeOptionalClass,
    case #FakeOptionalClass.None!enumelt: bb1,
    case #FakeOptionalClass.Some!enumelt.1: bb2

bb1:
  br bb3

bb2:
  br bb3

bb3:
  %1 = builtin_function_ref "sadd_with_overflow_Word" : $@thin (Builtin.Word, Builtin.Word, Builtin.Int1) -> (Builtin.Word, Builtin.Int1)
  %2 = integer_literal $Builtin.Word, 2
  %3 = integer_literal $Builtin.Word, 3
  %4 = integer_literal $Builtin.Int1, 0
  %5 = apply %1(%2, %3, %4) : $@thin (Builtin.Word, Builtin.Word, Builtin.Int1) -> (Builtin.Word, Builtin.Int1)
  release_value %0 : $FakeOptionalClass
  %9998 = function_ref @unknown : $@thin () -> ()
  apply %9998() : $@thin () -> ()
  release_value %0 : $FakeOptionalClass
  %9999 = tuple()
  return %9999 : $()
}