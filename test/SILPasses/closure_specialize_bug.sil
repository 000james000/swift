// RUN: %target-sil-opt -closure-specialize %s | FileCheck %s

// rdar://18156518

import Builtin
import Swift

protocol P {
  func foo(f: Int32->Int32, _ j: Int32) -> Int32
}

sil @_TF4test3bazfSiFT1mSi_Si : $@thin (Int32, Int32) -> Int32

// CHECK-LABEL: @_TF4test3barFTPS_1P_Si_Si : $@thin (@in P, Int32) -> Int32 {
// CHECK: partial_apply
// CHECK: apply
sil [noinline] @_TF4test3barFTPS_1P_Si_Si : $@thin (@in P, Int32) -> Int32 {
bb0(%0 : $*P, %1 : $Int32):
  %2 = open_existential %0 : $*P to $*@opened("01234567-89ab-cdef-0123-000000000000") P
  %3 = witness_method $@opened("01234567-89ab-cdef-0123-000000000000") P, #P.foo!1, %2 : $*@opened("01234567-89ab-cdef-0123-000000000000") P : $@thin @cc(witness_method) @callee_owned <T: P> (@owned @callee_owned (Int32) -> Int32, Int32, @inout T) -> Int32
  %4 = integer_literal $Builtin.Int32, 2
  %5 = struct $Int32 (%4 : $Builtin.Int32)
  // function_ref test.baz (Swift.Int32)(m : Swift.Int32) -> Swift.Int32
  %6 = function_ref @_TF4test3bazfSiFT1mSi_Si : $@thin (Int32, Int32) -> Int32
  %7 = partial_apply %6(%5) : $@thin (Int32, Int32) -> Int32
  %8 = apply %3<@opened("01234567-89ab-cdef-0123-000000000000") P>(%7, %1, %2) : $@thin @cc(witness_method) @callee_owned <T: P> (@owned @callee_owned (Int32) -> Int32, Int32, @inout T) -> Int32
  destroy_addr %0 : $*P
  return %8 : $Int32
}

// Test a specialized closure's indirect result type.
// SIL verification should catch the incorrect type.
// rdar:://19321284

sil hidden [fragile] @callee : $@thin (Builtin.Int32) -> () {
bb0(%0 : $Builtin.Int32):
  unreachable
}

sil hidden [fragile] @thunk : $@thin (@out (), @owned @callee_owned ()->()) -> () {
bb0(%0 : $*(), %1 : $@callee_owned () -> ()):
  unreachable
}

// CHECK-LABEL: @test_closure_propagation : $@thin () -> () {
// CHECK-NOT: partial_apply
sil hidden [fragile] @test_closure_propagation : $@thin () -> () {
bb0:
  %f1 = function_ref @callee : $@thin (Builtin.Int32) -> ()
  %i1 = integer_literal $Builtin.Int32, 24
  %p1 = partial_apply %f1(%i1) : $@thin (Builtin.Int32) -> ()

  %f2 = function_ref @thunk : $@thin (@out (), @owned @callee_owned () -> ()) -> ()
  %s1 = alloc_stack $()
  %a1 = apply [transparent] %f2(%s1#1, %p1) : $@thin (@out (), @owned @callee_owned () -> ()) -> ()
  dealloc_stack %s1#0 : $*@local_storage ()
  unreachable
}
