// RUN: %sil-opt -arc-opts %s | FileCheck %s

import Builtin

/////////////////
// Basic Tests //
/////////////////

// CHECK-LABEL: sil @simple_retain_release_pair : $@thin (Builtin.ObjectPointer) -> ()
// CHECK: bb0
// CHECK-NEXT: tuple()
// CHECK-NEXT: return
sil @simple_retain_release_pair : $@thin (Builtin.ObjectPointer) -> () {
bb0(%0 : $Builtin.ObjectPointer):
  strong_retain %0 : $Builtin.ObjectPointer
  strong_release %0 : $Builtin.ObjectPointer
  %1 = tuple()
  return %1 : $()
}

struct S {
  var x : Builtin.ObjectPointer
}

// CHECK-LABEL: sil @simple_copyvalue_destroyvalue_pair : $@thin (Builtin.ObjectPointer) -> ()
// CHECK: bb0
// CHECK-NEXT: return
sil @simple_copyvalue_destroyvalue_pair : $@thin (S) -> (S) {
bb0(%0 : $S):
  %1 = copy_value %0 : $S
  destroy_value %1 : $S
  %2 = copy_value %0 : $S
  destroy_value %0 : $S
  %3 = copy_value %1 : $S
  destroy_value %0 : $S
  return %2 : $S
}

sil @move_retain_over_non_side_effect_instructions : $@thin (Builtin.ObjectPointer, Builtin.Int64) -> (Builtin.Int1) {
bb0(%0 : $Builtin.ObjectPointer, %1 : $Builtin.Int64):
  strong_retain %0 : $Builtin.ObjectPointer
  %2 = builtin_function_ref "cmp_eq_Int64" : $@thin (Builtin.Int64, Builtin.Int64) -> Builtin.Int1
  %3 = apply %2(%1, %1) : $@thin (Builtin.Int64, Builtin.Int64) -> Builtin.Int1
  strong_release %0 : $Builtin.ObjectPointer
  return %3 : $Builtin.Int1
}

sil @move_copyvalue_over_non_side_effect_instructions : $@thin (S, Builtin.Int64) -> (Builtin.Int1) {
bb0(%0 : $S, %1 : $Builtin.Int64):
  copy_value %0 : $S
  %2 = builtin_function_ref "cmp_eq_Int64" : $@thin (Builtin.Int64, Builtin.Int64) -> Builtin.Int1
  %3 = apply %2(%1, %1) : $@thin (Builtin.Int64, Builtin.Int64) -> Builtin.Int1
  destroy_value %0 : $S
  return %3 : $Builtin.Int1
}

sil @dont_move_retain_over_non_side_effect_instructions : $@thin (Builtin.ObjectPointer, Builtin.ObjectPointer) -> () {
bb0(%0 : $Builtin.ObjectPointer, %1 : $Builtin.ObjectPointer):
  strong_retain %0 : $Builtin.ObjectPointer
  strong_release %1 : $Builtin.ObjectPointer
  strong_release %0 : $Builtin.ObjectPointer
  %2 = tuple()
  return %2 : $()
}

sil @dont_move_copyvalue_over_non_side_effect_instructions : $@thin (S, Builtin.ObjectPointer) -> () {
bb0(%0 : $S, %1 : $Builtin.ObjectPointer):
  copy_value %0 : $S
  strong_release %1 : $Builtin.ObjectPointer
  destroy_value %0 : $S
  %2 = tuple()
  return %2 : $()
}
