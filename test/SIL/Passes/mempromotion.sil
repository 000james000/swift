// RUN: %swift %s -emit-sil | FileCheck %s

import Builtin
import swift


// CHECK: sil @simple_promotion
sil @simple_promotion : $(x : Int64) -> Int64 {
bb0(%0 : $Int64):
  %1 = alloc_box $Int64
  %2 = store %0 to %1#1 : $*Int64
  %3 = load %1#1 : $*Int64
  %4 = release %1#0 : $Builtin.ObjectPointer
  %5 = return %3 : $Int64

// CHECK: alloc_stack
// CHECK-NOT: alloc_box
// CHECK-NOT: release
// CHECK: return
}

// CHECK: sil @init_var
sil @init_var : $() -> Int64 {
bb0:
  %1 = alloc_box $Int64
  %2 = initialize_var %1#1 : $*Int64
  %3 = load %1#1 : $*Int64
  %4 = release %1#0 : $Builtin.ObjectPointer
  %5 = return %3 : $Int64

// CHECK: %0 = alloc_stack
// CHECK-NOT: alloc_box
// CHECK-NOT: release
// CHECK-NOT: destroy_addr
// CHECK: dealloc_stack %0 : $*Int64
// CHECK: return
}

// CHECK: sil @multi_release
sil @multi_release : $() -> Int64 {
bb0:
  %1 = alloc_box $Int64
  %2 = initialize_var %1#1 : $*Int64
  %3 = load %1#1 : $*Int64
  %x = retain %1#0 : $Builtin.ObjectPointer
  %y = release %1#0 : $Builtin.ObjectPointer
  %b = br bb1
bb1:

  %4 = release %1#0 : $Builtin.ObjectPointer
  %5 = return %3 : $Int64

// CHECK: %0 = alloc_stack
// CHECK: bb1:
// CHECK: dealloc_stack %0 : $*Int64
// CHECK: return
}

struct TestStruct {
  var Elt : Int
}

// CHECK: sil @struct_tuple_element_addr
sil @struct_tuple_element_addr : $(x : Int64) -> Int64 {
bb1(%0 : $Int64):
  %1 = alloc_box $TestStruct
  %a = alloc_box $(Int, Int)
  
  %2 = struct_element_addr %1#1 : $*TestStruct, @Elt
  %3 = store %0 to %2 : $*Int64

  %b = tuple_element_addr %a#1 : $*(Int, Int), 0
  %c = store %0 to %b : $*Int

  %6 = struct_element_addr %1#1 : $*TestStruct, @Elt
  %7 = load %6 : $*Int64
  %8 = release %1#0 : $Builtin.ObjectPointer
  %x = release %a#0 : $Builtin.ObjectPointer


  %9 = return %7 : $Int64

// CHECK: %1 = alloc_stack $TestStruct
// CHECK: %2 = alloc_stack $(Int64, Int64)
// CHECK: dealloc_stack %1
// CHECK: dealloc_stack %2
// CHECK: return
}




sil @callee : $[thin] (a : [byref] Int64) -> ()

// CHECK: sil @byref_nocapture
sil @byref_nocapture : $[thin] () -> Int64 {
bb0:
  // CHECK: alloc_stack
  %1 = alloc_box $Int64
  %6 = function_ref @callee : $[thin] (a : [byref] Int64) -> ()
  %7 = apply %6(%1#1) : $[thin] (a : [byref] Int64) -> ()
  %8 = load %1#1 : $*Int64
  %9 = release %1#0 : $Builtin.ObjectPointer
  %10 = return %8 : $Int64
  // CHECK: return
}


protocol P {
}

sil @returns_protocol : $[thin] () -> P

// CHECK: sil @test_indirect_return
sil @test_indirect_return : $[thin] () -> () {
bb0:
  // CHECK: alloc_stack
  %1 = function_ref @returns_protocol : $[thin] () -> P
  %2 = alloc_box $P
  %3 = apply %1(%2#1) : $[thin] () -> P
  %5 = release %2#0 : $Builtin.ObjectPointer
  %6 = return %0 : $()
  %0 = tuple ()
  // CHECK: return
}

class SomeClass {}

// CHECK: sil @class_promotion
sil @class_promotion : $(x : SomeClass) -> SomeClass {
bb0(%0 : $SomeClass):
  %1 = alloc_box $SomeClass
  %2 = store %0 to %1#1 : $*SomeClass
  %3 = load %1#1 : $*SomeClass
  %4 = release %1#0 : $Builtin.ObjectPointer
  %5 = return %3 : $SomeClass

// CHECK: %1 = alloc_stack
// CHECK-NOT: alloc_box
// CHECK-NOT: release
// CHECK: release {{.*}} : $SomeClass
// CHECK: return
}
