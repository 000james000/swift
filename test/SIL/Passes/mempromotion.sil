// RUN: %swift %s -emit-sil | FileCheck %s

import Builtin
import swift


// CHECK: sil @simple_promotion
sil @simple_promotion : $(x : Int64) -> Int64 {
bb0(%0 : $Int64):
  %1 = alloc_box $Int64
  %2 = store %0 to %1#1 : $*Int64
  %3 = load %1#1 : $*Int64
  %4 = release %1#0 : $Builtin.ObjectPointer
  %5 = return %3 : $Int64

// CHECK: alloc_stack
// CHECK-NOT: alloc_box
// CHECK-NOT: release
// CHECK: return
}

// CHECK: sil @init_var
sil @init_var : $() -> Int64 {
bb0:
  %1 = alloc_box $Int64
  %2 = initialize_var %1#1 : $*Int64
  %3 = load %1#1 : $*Int64
  %4 = release %1#0 : $Builtin.ObjectPointer
  %5 = return %3 : $Int64

// CHECK: %0 = alloc_stack
// CHECK-NOT: alloc_box
// CHECK-NOT: release
// CHECK: destroy_addr %0 : $*Int64
// CHECK: dealloc_stack %0 : $*Int64
// CHECK: return
}

// CHECK: sil @multi_release
sil @multi_release : $() -> Int64 {
bb0:
  %1 = alloc_box $Int64
  %2 = initialize_var %1#1 : $*Int64
  %3 = load %1#1 : $*Int64
  %x = retain %1#0 : $Builtin.ObjectPointer
  %y = release %1#0 : $Builtin.ObjectPointer
  %b = br bb1
bb1:

  %4 = release %1#0 : $Builtin.ObjectPointer
  %5 = return %3 : $Int64

// CHECK: %0 = alloc_stack
// CHECK: bb1:
// CHECK: dealloc_stack %0 : $*Int64
// CHECK: return
}

struct TestStruct {
  var Elt : Int
}

// CHECK: sil @struct_element_addr
sil @struct_element_addr : $(x : Int64) -> Int64 {
bb1(%0 : $Int64):
  %1 = alloc_box $TestStruct
  
  %2 = struct_element_addr %1#1 : $*TestStruct, @Elt
  %3 = store %0 to %2 : $*Int64

  %6 = struct_element_addr %1#1 : $*TestStruct, @Elt
  %7 = load %6 : $*Int64
  %8 = release %1#0 : $Builtin.ObjectPointer
  %9 = return %7 : $Int64

// CHECK: %1 = alloc_stack
// CHECK: dealloc_stack %1
// CHECK: return
}




sil @callee : $[thin] (a : [byref] Int64) -> ()

// CHECK: sil @byref_nocapture
sil @byref_nocapture : $[thin] () -> Int64 {
bb0:
  // CHECK: alloc_stack
  %1 = alloc_box $Int64
  %6 = function_ref @callee : $[thin] (a : [byref] Int64) -> ()
  %7 = apply %6(%1#1) : $[thin] (a : [byref] Int64) -> ()
  %8 = load %1#1 : $*Int64
  %9 = release %1#0 : $Builtin.ObjectPointer
  %10 = return %8 : $Int64
  // CHECK: return
}




